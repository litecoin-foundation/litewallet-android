apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

apply plugin: 'com.google.gms.google-services'

apply plugin: 'com.google.firebase.crashlytics'

configurations {
    ktlint
}

android {
    signingConfigs {
        release
    }
    Properties props = new Properties()
    def propFile = file('../release.properties')
    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))

        if (props != null && props.containsKey('storeFile') && props.containsKey('storePassword') &&
                props.containsKey('keyAlias') && props.containsKey('keyPassword')) {

            android.signingConfigs.release.storeFile = file(props['storeFile'])
            android.signingConfigs.release.storePassword = props['storePassword']
            android.signingConfigs.release.keyAlias = props['keyAlias']
            android.signingConfigs.release.keyPassword = props['keyPassword']
        }
    }

    compileSdkVersion = 29

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    defaultConfig {
        testInstrumentationRunner = 'androidx.test.runner.AndroidJUnitRunner'
        applicationId = 'com.loafwallet'
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 640
        versionName "v2.3.1"
        multiDexEnabled true

        buildConfigField "String", "INFURA_KEY", "\"set the infura key here\""

        // Similar to other properties in the defaultConfig block,
        // you can configure the ndk block for each product flavor
        // in your build configuration.
        ndk {
            // Specifies the ABI configurations of your native
            // libraries Gradle should build and package with your APK.
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_TOOLCHAIN=clang"
            }
        }

    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    packagingOptions {
        pickFirst 'protobuf.meta'
    }
    externalNativeBuild {
        cmake {
            // When you specify a version of CMake, as shown below,
            // the Android plugin searches for its binary within your
            // PATH environmental variable.
            path "CMakeLists.txt"  //path can only be set outside (in android block)

        }
    }
    // Specifies two flavor dimensions.
    flavorDimensions "mode"
    productFlavors {
        loaf {
            applicationId "com.loafwallet"
            dimension "mode"
            resValue "string", "app_name", "Litewallet"
            buildConfigField "boolean", "LITECOIN_TESTNET", "false"

            externalNativeBuild {
                cmake {
                    // When you specify a version of CMake, as shown below,
                    // the Android plugin searches for its binary within your
                    // PATH environmental variable.
                    cFlags "-DLITECOIN_TESTNET=0"
                    targets "core-lib"
                }
            }
        }

        loafTestnet {
            applicationId "com.loafwallet.testnet"
            dimension "mode"
            resValue "string", "app_name", "Litewallet-testnet"
            buildConfigField "boolean", "LITECOIN_TESTNET", "true"

            externalNativeBuild {
                cmake {
                    // When you specify a version of CMake, as shown below,
                    // the Android plugin searches for its binary within your
                    // PATH environmental variable.
                    cFlags "-DLITECOIN_TESTNET=1"
                    targets "core-lib"
                }
            }
        }
    }
    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet false
        explainIssues true

        // if true, only report errors
        ignoreWarnings false

        checkReleaseBuilds false

        // Turns off checks for the issue IDs you specify.
        //example: disable 'TypographyFractions','TypographyQuotes'
        // Turns on checks for the issue IDs you specify. These checks are in
        // addition to the default lint checks.
        //example: enable 'RtlHardcoded','RtlCompat', 'RtlEnabled'
        // To enable checks for only a subset of issue IDs and ignore all others,
        // list the issue IDs with the 'check' property instead. This property overrides
        // any issue IDs you enable or disable using the properties above.
        check 'NewApi', 'InlinedApi'
    }

    buildTypes {
//        gitUpdateModules()
//        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        release {
            signingConfig signingConfigs.release
            minifyEnabled = false
            debuggable = false

            ndk {
                minifyEnabled = false
                debuggable = false
            }

            firebaseCrashlytics {
                // Enable processing and uploading of native symbols to Crashlytics
                // servers. By default, this is disabled to improve build speeds.
                // This flag must be enabled to see properly-symbolicated native
                // stack traces in the Crashlytics dashboard.
                nativeSymbolUploadEnabled true
            }
        }
        debug {
            debuggable = true
            minifyEnabled = false

            ndk {
                minifyEnabled = false
                debuggable = true
            }
        }

    }
    compileSdkVersion 29
}

task copyResDirectoryToClasses(type: Copy) {
    from "${projectDir}/src/test/res"
    into "${buildDir}/intermediates/classes/test/debug/res"
}

gradle.projectsEvaluated {
    assembleDebug.dependsOn(copyResDirectoryToClasses)
}


//def gitUpdateModules() {
//    ['sh', '-c', 'git submodule foreach git pull origin master'].execute().text.trim()
//    getLogger().debug("gitUpdateModules done!")
//}

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation 'commons-io:commons-io:2.4'
    implementation 'org.eclipse.jetty:jetty-webapp:9.2.19.v20160908'
    implementation 'org.eclipse.jetty.websocket:websocket-server:9.2.19.v20160908'
    implementation 'org.eclipse.jetty:jetty-continuation:9.2.19.v20160908'
    implementation 'org.slf4j:slf4j-api:1.7.22'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.squareup.okhttp3:okhttp:4.3.1'
    implementation 'io.sigpipe:jbsdiff:1.0'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.test.ext:junit:1.1.1'
    implementation 'androidx.test:rules:1.2.0'
    implementation 'junit:junit:4.12'
    implementation 'androidx.test.espresso:espresso-core:3.2.0'

    // add the Firebase SDK for Google Analytics
    implementation 'com.google.firebase:firebase-analytics:18.0.0'

    // Firebase Crashlytics.
    implementation 'com.google.firebase:firebase-crashlytics:17.3.0'

    // Firebase Crashlytics NDK
    implementation 'com.google.firebase:firebase-crashlytics-ndk:17.3.0'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Unstoppable domain
    implementation 'com.unstoppabledomains:resolution:1.8.1'

    // Progress Button
    implementation 'com.github.razir.progressbutton:progressbutton:2.1.0'

    implementation "androidx.core:core-ktx:1.3.2"

    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'

    // ChromeCustomTabs
    implementation "androidx.browser:browser:1.3.0"

    api project(":partnerapi")

    // Dagger
    implementation 'com.google.dagger:dagger:2.27'
    kapt 'com.google.dagger:dagger-compiler:2.27'

    // Play Core
    implementation 'com.google.android.play:core:1.10.0'

    // Play Core KTX
    implementation 'com.google.android.play:core-ktx:1.8.1'

    // Ktlint
    ktlint "com.pinterest:ktlint:0.37.2"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
